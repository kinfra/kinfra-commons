plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'ru.kontur.kinfra.presets' version '1.5'
}

ext {
    versions = [
        assertj   : '3.14.0',
        coroutines: '1.4.3',
    ]
}

group = "ru.kontur.kinfra"
version = "1.7"

dependencies {
    implementation platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:${versions.coroutines}")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8"

    testImplementation "org.assertj:assertj-core:${versions.assertj}"
}

kotlin {
    explicitApi()
}

jar {
    manifest.attributes "Automatic-Module-Name": "ru.kontur.kinfra.commons"
}

publishing {
    repositories {
        def properties = project.properties
        def repoUrl = properties["publishing.repo.url"]
        if (repoUrl) {
            maven {
                url = repoUrl

                credentials {
                    username = properties["publishing.repo.username"]
                    password = properties["publishing.repo.password"]
                }
            }
        }
    }
}
